buildscript{
  repositories{
    mavenCentral()
    mavenLocal()
  }
}

allprojects{
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'idea'

  repositories{
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    mavenLocal()
  }

  /** needed to disable Java 8 doclint which throws errors **/
  tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
  }
}

ext{
  projectGroup = 'pathfinding'
  gdxVersion = '1.2.0'
  distDir = file('dist/')   
}


subprojects{
  // Binary Jar
  jar {
    destinationDir      = distDir
  }

  task javadocJar(type: Jar, dependsOn:javadoc){
    classifier = 'javadoc'
    from javadoc, sourceSets.main.allSource
    destinationDir      = distDir
  }

  task sourcesJar(type: Jar, dependsOn:classes){
    classifier = 'sources'
    from sourceSets.main.allSource
    destinationDir      = distDir
  }

  buildDir = rootProject.buildDir


}

project(':pathfinding'){
	apply from : '../publish.gradle'

	dependencies { 
		testCompile 'junit:junit:4.10'
	}
}

project(':gdx-bridge'){
	apply from : '../publish.gradle'

   dependencies {
   	compile project(":pathfinding")
   	compile "com.badlogicgames.gdx:gdx:$gdxVersion"
  }
}

project(':tests'){
	dependencies {
    	compile project(":pathfinding")
    	compile project(":gdx-bridge")
    	compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    	compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
      compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
  	}
}



task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

tasks.eclipse.doLast {
    delete ".project"
}

